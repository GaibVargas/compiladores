PROGRAM  ->  STATEMENT  
PROGRAM  ->  FUNCLIST  
PROGRAM  ->  e
 
FUNCLIST  ->  FUNCDEF FUNCLIST  
FUNCLIST  ->  FUNCDEF
 
FUNCDEF  ->  def ident(PARAMLIST){STATELIST}
 
PARAMLIST  ->  int ident, PARAMLIST  
PARAMLIST  ->  float ident, PARAMLIST  
PARAMLIST  ->  string ident, PARAMLIST  
PARAMLIST  ->  int ident  
PARAMLIST  ->  float ident  
PARAMLIST  ->  string ident  
PARAMLIST  ->  e
 
STATEMENT  ->  VARDECL;  
STATEMENT  ->  ATRIBSTAT;  
STATEMENT  ->  PRINTSTAT;  
STATEMENT  ->  READSTAT;  
STATEMENT  ->  RETURNSTAT;  
STATEMENT  ->  IFSTAT  
STATEMENT  ->  FORSTAT  
STATEMENT  ->  {STATELIST}  
STATEMENT  ->  break;  
STATEMENT  ->  ;
 
INDEX  ->  [int_contant]  
INDEX  ->  [int_constant]INDEX
 
VARDECL  ->  int ident  
VARDECL  ->  float ident  
VARDECL  ->  string ident  
VARDECL  ->  int ident INDEX  
VARDECL  ->  float ident INDEX  
VARDECL  ->  string ident INDEX
 
ATRIBSTAT  ->  LVALUE = EXPRESSION  
ATRIBSTAT  ->  LVALUE = ALLOCEXPRESSION  
ATRIBSTAT  ->  LVALUE = FUNCCALL
 
FUNCCALL  ->  ident(PARAMLISTCALL)
 
PARAMLISTCALL  ->  ident, PARAMLISTCALL  
PARAMLISTCALL  ->  ident  
PARAMLISTCALL  ->  e
 
PRINTSTAT  ->  print EXPRESSION
 
READSTAT  ->  read LVALUE
 
RETURNSTAT  ->  return
 
IFSTAT  ->  if(EXPRESSION) STATEMENT  
IFSTAT  ->  if (EXPRESSION) STATEMENT else STATEMENT
 
FORSTAT  ->  for(ATRIBSTAT; EXPRESSION; ATRIBSTAT) STATEMENT
 
STATELIST  ->  STATEMENT STATELIST  
STATELIST  ->  STATEMENT
 
ALLOCAUX  ->  [NUMEXPRESSION] ALLOCAUX  
ALLOCAUX  ->  [NUMEXPRESSION]
 
ALLOCEXPRESSION  ->  new int ALLOCAUX  
ALLOCEXPRESSION  ->  new float ALLOCAUX  
ALLOCEXPRESSION  ->  new string ALLOCAUX
 
RELOP  ->  <  
RELOP  ->  >  
RELOP  ->  <=  
RELOP  ->  >=  
RELOP  ->  ==  
RELOP  ->  !=
 
EXPRESSION  ->  NUMEXPRESSION  
EXPRESSION  ->  NUMEXPRESSION RELOP NUMEXPRESSION
 
NUMEXPRESSIONAUX  ->  +NUMEXPRESSION  
NUMEXPRESSIONAUX  ->  -NUMEXPRESSION
 
NUMEXPRESSION  ->  TERM  
NUMEXPRESSION  ->  TERM NUMEXPRESSIONAUX
 
TERM  ->  UNARYEXPR  
TERM  ->  UNARYEXPR UNARYEXPRAUX
 
UNARYEXPRAUX  ->  *UNARYEXPR  
UNARYEXPRAUX  ->  /UNARYEXPR  
UNARYEXPRAUX  ->  %UNARYEXPR
 
UNARYEXPR  ->  +FACTOR  
UNARYEXPR  ->  -FACTOR  
UNARYEXPR  ->  FACTOR
 
FACTOR  ->  int_constant  
FACTOR  ->  float_constant  
FACTOR  ->  string_constant  
FACTOR  ->  null  
FACTOR  ->  LVALUE  
FACTOR  ->  (NUMEXPRESSION)
 
LVALUE  ->  ident  
LVALUE  ->  ident ALLOCAUX 
